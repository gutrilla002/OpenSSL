#! /usr/bin/env perl
# Copyright 2021 The OpenSSL Project Authors. All Rights Reserved.
#
# Licensed under the Apache License 2.0 (the "License").  You may not use
# this file except in compliance with the License.  You can obtain a copy
# in the file LICENSE in the source distribution or at
# https://www.openssl.org/source/license.html

# Run this program like this:
#       perl util/find-include-nits [files...]
#
# If given no arguments, will open a pipe to 'git ls-files'

use strict;
use warnings;

my $status = 0;

# Print error message, set $status.
sub err {
    print join(" ", @_), "\n";
    $status = 1
}

sub
process
{
    my $file = shift;

    # If it's foo.in, reach foo
    my $infile = $file;
    $infile =~ s/\.in$//;

    my $IN;
    if ( !open($IN, '<', $infile) ) {
        err("Can't open $infile, $!");
        return;
    }

    while ( <$IN> ) {
        chomp;
        next unless /^#\s*include(\s+)(["<])([^">]+)/;
        my $spaces = $1;
        my $quotes = $2;
        my $included = $3;
        err($infile, "excess spaces after include")
            if $spaces =~ /  /;
        if ( $quotes eq '<' ) {
            err($infile, "use quotes for include", $included)
                if $included =~ m@crypto/@
                    || $included =~ m@internal/@
                    || $included =~ m@provider/@
                    || $included =~ m@prov/@;
        } elsif ( $quotes eq '"' ) {
            err($infile, "Use <> for include", $included)
                if $included =~ m@openssl/@;
        }
    }

    close $IN;
}

## MAIN

if ( @ARGV ) {
    foreach my $f ( @ARGV ) {
        process($f);
    }
} else {
    open my $PIPE, "git ls-files|" or die "Can't open git, $!";

    while ( <$PIPE> ) {
        chomp;
        next unless /\.[ch]$/ or /\.[ch]\.in$/;
        process($_);
    }
}

exit $status;
